#!/usr/bin/perl

# echo-loop
# John Jacobsen, NPX Designs, Inc., jacobsen\@npxdesigns.com
# Started: Mon Jul 18 12:12:01 2005

package MY_PACKAGE;
use strict;
use Getopt::Long;

my $echotest = "/usr/local/share/domhub-testing/echo-test";
die "$0: echo test program $echotest not installed, need domhub-tools RPM.\n"
    unless -x $echotest;

sub usage { 
    return <<EOF;
Usage: $0 -n msgs <dom> ....
EOF
;
}

my $nmsgs = 0;
my $help;

GetOptions("help|h"          => \$help,
	   "n=i"             => \$nmsgs) or die usage;

my $doms = join ' ', @ARGV;

die usage if $help;
select STDOUT; $|++;


sub dochunk { 
    
    return unless $nmsgs > 0;
    my $delta = shift; die unless defined $delta;
    my $chunk = $delta;
    if($nmsgs < $delta) {
        $chunk = $nmsgs;
    }
    my $cmd = "$echotest -n $chunk $doms";
    print "$chunk msgs:\n";

    # Recipe from the Perl Cookbook, 16.21, p. 595
    my $result;
    $SIG{ALRM} = sub { die "timeout\n";};
    my $deathtime = 320;
    eval {
	alarm($deathtime);
	$result = `$cmd 2>&1`;
	alarm(0);
    };

    if($@) {
	if($@ =~ /timeout/i) {
	    my $now = time." (".(scalar localtime).")"; 
	    die "echo-loop ERROR: timeout (>$deathtime seconds) ".
		"in $echotest at $now (DOM list = $doms)!\n"; 
	} else {
	    alarm(0);
	    die; # Propagate the exception
	}
    }

    if($result =~ /echo-test/) {
	warn $result;
	die "echo-loop ERROR: echo-test gave the preceeding error message.\n";
    }

    print $result;

    $nmsgs -= $chunk;
}

dochunk 1;
dochunk 10;
dochunk 100;
while($nmsgs > 0) {
    dochunk 1000;
}

__END__

